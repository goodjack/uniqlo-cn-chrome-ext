group 'test'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.0'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

build.doLast {
    // Copy kotlin library files to the dist directory
    // Copy kotlin.js and kotlin-meta.js from jar into dist directory
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${projectDir}/dist"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }
    // Copy static resources to the dist directory
    copy {
        includeEmptyDirs = false
        from "${buildDir}/resources/main"
        into "${projectDir}/dist"
    }
}
// Delete the dist directory as part of the clean command
clean.doFirst {
    delete "${projectDir}/dist"
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19.3'
}
